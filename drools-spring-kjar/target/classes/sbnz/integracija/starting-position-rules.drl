package com.catoni;

import java.util.Collections;

import com.catoni.models.enums.MoveTypes;
import com.catoni.models.enums.ResourceTypes;
import com.catoni.models.enums.BuildingTypes;
import com.catoni.models.enums.Status;
import com.catoni.models.*;
import com.catoni.models.dto.*;
import java.lang.Number;


rule "Build House Start"
    lock-on-active true
    salience 100
    when
       $is: InputState()

       accumulate(
            Building($t: this, status == Status.FREE) from $is.position.buildings,
            $free: collectList($t)
       )

       Number($val: doubleValue) from accumulate(
            Building($t: this, status == Status.FREE) from $free,
            max($t.getSumOfChances())
       )

       accumulate(
            Building($tt: this, getSumOfChances() == $val) from $free,
            $best: collectList($tt)
       )

       $dtoB: BuildingDto()

    then

       $dtoB.setRow(((Building) $best.get(0)).getRow());
       $dtoB.setCol(((Building) $best.get(0)).getColumn());
       $dtoB.setType(BuildingTypes.HOUSE);
       $dtoB.setPlayerName("bot");

       $is.getPosition().addBuilding($dtoB);
       int numberOfHouses = $is.getPlayerStates().get("bot").getNumberOfHouses();
        $is.getPlayerStates().get("bot").setNumberOfHouses(numberOfHouses + 1);
       update($dtoB);
end

rule "Build Road Start"
    no-loop
    salience 50
    when
        $is: InputState()

        $b: BuildingDto(playerName == "bot")

        Number($val: doubleValue) from accumulate(
            Building($t: this) from $is.position.getTwoPlaceApart($b),
            max($t.getSumOfChances())
        )
        accumulate(
            Building($tt: this, getSumOfChances() == $val) from $is.position.getTwoPlaceApart($b),
            $br: collectList($tt)
        )
        $dtoR: RoadDto()
    then
        Road r = $is.getPosition().getRoadTowardsBuilding($b, (Building) $br.get(0));
        $dtoR.setRow1(r.getRow1());
        $dtoR.setCol1(r.getCol1());
        $dtoR.setRow2(r.getRow2());
        $dtoR.setCol2(r.getCol2());
        $dtoR.setPlayer("bot");

        $is.getPosition().addRoad($dtoR);
        update($dtoR);
end
